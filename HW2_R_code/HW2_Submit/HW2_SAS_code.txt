

/*HW2 Problem 1 */

/*multinomial logit*/
FILENAME REFFILE '/home/matthewlooney0/my_content/Econometrics3/multinomial.xlsx';
PROC IMPORT DATAFILE=REFFILE
	DBMS=XLSX
	OUT=multinomial;
	GETNAMES=YES;
RUN;
PROC CONTENTS DATA=multinomial; RUN;
proc logistic data=multinomial;
class choice(ref="1")/param=reference;
model choice =income size race / link=glogit;
run;

/*conditional logit*/
FILENAME REFFILE '/home/matthewlooney0/my_content/Econometrics3/conditional.xlsx';
PROC IMPORT DATAFILE=REFFILE
	DBMS=XLSX
	OUT=conditional;
	GETNAMES=YES;
RUN;
PROC CONTENTS DATA=conditional; RUN;
proc mdc data=conditional;
model choice=price d1 d2 d3 d4 d5 d6 d7 d8/type=clogit nchoice=8;
id id;
run;

/*mixed logit*/
FILENAME REFFILE '/home/matthewlooney0/my_content/Econometrics3/mixed.xlsx';
PROC IMPORT DATAFILE=REFFILE
	DBMS=XLSX
	OUT=mixed;
	GETNAMES=YES;
RUN;
PROC CONTENTS DATA=mixed; RUN;
data mixed;
set mixed;
d1inc=d1*income;
d2inc=d2*income;
d3inc=d3*income;
d4inc=d4*income;
d5inc=d5*income;
d6inc=d6*income;
d7inc=d7*income;
d8inc=d8*income;

d1size=d1*size;
d2size=d2*size;
d3size=d3*size;
d4size=d4*size;
d5size=d5*size;
d6size=d6*size;
d7size=d7*size;
d8size=d8*size;

d1race=d1*race;
d2race=d2*race;
d3race=d3*race;
d4race=d4*race;
d5race=d5*race;
d6race=d6*race;
d7race=d7*race;
d8race=d8*race;
run;
proc mdc data=mixed;
model choice=d1inc d2inc d3inc d3inc d5inc d6inc d7inc 
             d1size d2size d3size d4size d5size d6size d7size
			 d1race d2race d3race d4race d5race d6race d7race 
			 price_cl/type=mixedlogit nchoice=8;
ID pid;
run;

/*nested model*/
FILENAME REFFILE '/home/matthewlooney0/my_content/Econometrics3/nested.xlsx';
PROC IMPORT DATAFILE=REFFILE
	DBMS=XLSX
	OUT=nested;
	GETNAMES=YES;
RUN;
PROC CONTENTS DATA=nested; RUN;
proc mdc data=nested;
model choice=price c6 c7 /type=nestedlogit choice=(c 6 7 8,brchcl 1 2 3 4 5 6 7 8);
id id;
utility u(1,1 2 3@6)=price,
        u(1,4 5 @7)=price,
		u(1,6 7 8@8)=price,
		u(2,6 7 8)=c6 c7 ;
test INC_L2G1C6=1, INC_L2G1C7=1, INC_L2G1C8=1 /LR;
run;

%IIA( indata=nested,
      varlist=price c6 c7,
      nchoice=8,
      choice=choice,
      nvar=3,
      nIIA=2,
      IIA=(1 3),
      id=id,
      decision=choice );







/* Problem 2 */

/* Generated Code (IMPORT) */
/* Source File: CreditScoring.csv */
/* Source Path: /home/matthewlooney0/my_content/Econometrics3 */
/* Code generated on: 11/3/17, 11:59 PM */ 
FILENAME REFFILE 
	'/home/matthewlooney0/my_content/Econometrics3/CreditScoring.csv';
PROC IMPORT DATAFILE=REFFILE DBMS=CSV OUT=credit;
	GETNAMES=YES;
RUN;
PROC CONTENTS DATA=credit;
RUN;
data credit;
	set credit;
	lnincome=log(income);
run;
data holders;
	set credit;

	if logspend=. then
		delete;
run;

/* Problem 2.1.a */
title1 'Estimating by OLS';
proc reg data=credit;
	model logspend=lnincome age adepcnt ownrent;
	ods graphics off;
	run;

/* Problem 2.1.b */
title2 'Estimating a Tobit model - censored';
proc qlim data=credit;
model logspend = lnincome age adepcnt ownrent;
endogenous logspend ~ censored (lb=0);
output out=out_tobit_cens residual marginal;
ods graphics off;
run;
 proc means data=out_tobit_cens;
    var Meff_lnincome Meff_age Meff_adepcnt Meff_ownrent;
    run;
  
/* Problem 2.1.c */

/*Heckman 2-step*/
/*first step*/
title3 'Heckman two-step';
proc logistic data=credit descending;
	model cardhldr=lnincome age adepcnt ownrent /link=probit;
	output out=credit xbeta=Xbeta;
	ods graphics off;
run;

data credit;
	set credit;
	phi=pdf('normal', xbeta);
	bigphi=cdf('normal', xbeta);
	lambda=phi/bigphi;
run;

/*second step*/
proc reg data=credit outest=coef;
	model logspend=lnincome age adepcnt ownrent lambda;
	ods graphics off;
	run;

data coef;
	set coef;
	b1=intercept;
	b2=lnincome;
	b3=age;
	b4=adepcnt;
	b5=ownrent;
	b6=lambda;
	keep b1 b2 b3 b4 b5 b6;
run;

proc means data=credit noprint;
	var lnincome age adepcnt ownrent phi bigphi lambda xbeta;
	output out=avge mean=incomebar agebar adbar orbar phibar bigphibar lambdabar 
		xbetabar;
run;

data avge;
	set avge;
	keep incomebar agebar adbar orbar phibar bigphibar lambdabar xbetabar;
run;

data ME;
	set avge;
	set coef;
	MEincome=b2-b2*b6*lambdabar*xbetabar*(1-lambdabar);
	keep Meincome;
run;



/* 2.2.a */
/* OLS of subset data */



/* Problem 2.2.b */
title1 'Estimating a Tobit model - truncated';
proc qlim data=holders;
model logspend = lnincome age adepcnt ownrent;
endogenous logspend ~ truncated (lb=0);
output out=out_tobit_trunc residual marginal;
ods graphics off;
run;
 proc means data=out_tobit_trunc;
    var Meff_lnincome Meff_age Meff_adepcnt Meff_ownrent;
    run;


proc freq data=holders;
table majordrg;
run;

/* Problem 2.3.a*/
proc genmod data=holders; 
model  majordrg= lnincome age adepcnt ownrent EXP_Inc/ dist = poisson;
output out=real_poisson pre=phat_poisson;
run;

/* Problem 2.3.b*/
proc genmod data=holders;
model majordrg = lnincome age adepcnt ownrent EXP_Inc/ dist = negbin;
output out=real_negbin pre=phat_negbin;
run;

/* Problem 2.3.c*/

proc fmm data=holders cov;
         model  majordrg= lnincome age adepcnt ownrent EXP_Inc/ dist=truncpoisson;
         ods output parameterestimates=pe cov=covb;
         run;

proc fmm data=holders cov;
         model  majordrg= lnincome age adepcnt ownrent EXP_Inc/ dist=truncnegbin;
         ods output parameterestimates=pe1 cov=covb1;
         run;




